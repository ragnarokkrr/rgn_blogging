buildscript {
	ext {
        springBootVersion = "1.3.3.RELEASE"
        osPackageVersion = "3.5.0"
    }
	repositories {
		jcenter()
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "com.netflix.nebula:gradle-ospackage-plugin:${osPackageVersion}"
	}
}


apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'nebula.ospackage'


repositories {
	mavenCentral()
}

group = 'ragna'
version = '0.1.0'
mainClassName = 'ragna.goodbye.Application'


sourceCompatibility = 1.8
targetCompatibility = 1.8


dependencies {
	compile("org.springframework.boot:spring-boot-starter-web")
	testCompile("junit:junit")
}

distributions {
    main {
        baseName = 'ragna-goodbye'
        version = "${project.version}"

    }
}

jar {
	baseName = 'ragna-goodbye'
	version = "${project.version}"
    manifest {
        attributes("Implementation-Title": "Ragna Service",
            "Implementation-Version": "${project.version}")
    }
}

springBoot {
	// Allows us to run the jar directly from the command line, e.g. ./daemon-demo-0.0.1.jar
	executable = true
	// Excludes the devtools package from production builds
	excludeDevtools = true
}

ospackage {
	packageName = 'ragna-goodbye'
	version = "${project.version}"
	release = '1'
	//arch = NOARCH
	type = BINARY
	os = LINUX


	preInstall file("scripts/rpm/preInstall.sh")
	postInstall file("scripts/rpm/postInstall.sh")
	preUninstall file("scripts/rpm/preUninstall.sh")
	postUninstall file("scripts/rpm/postUninstall.sh")

	into "/opt/local/ragna-goodbye"
	user "ragna-service"
	permissionGroup "ragna-service"


	// Copy the actual .jar file
	from(jar.outputs.files) {
		// Strip the version from the jar filename
		rename { String fileName ->
			fileName.replace("-${project.version}", "")
		}
		fileMode 0500
		into "bin"
	}

	// Copy the config files
	from("install/linux/conf") {
		fileType CONFIG | NOREPLACE
		fileMode 0754
		into "conf"
	}
}


buildDeb {
	user "ragna-service"
	permissionGroup "ragna-service"
	directory("/opt/local/ragna-goodbye/log", 0755)
	link("/etc/init.d/ragna-goodbye", "/opt/local/ragna-goodbye/bin/ragna-goodbye.jar")
	link("/opt/local/ragna-goodbye/bin/ragna-goodbye.conf", "/opt/local/ragna-goodbye/conf/ragna-goodbye.conf")
}


task wrapper(type: Wrapper) {
	gradleVersion = '2.3'
}


